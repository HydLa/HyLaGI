X := {x1..x2}.
Y := {y1..y2}.
S := {s1..s2}.
Time := {t1..t2}.
Xinit := {-10000,10000}.
Yinit := {0,0}.
Sinit := { 0, Pi }.

INIT(x,y,s,time,x0,y0,s0) <=> x = x0 /\ y = y0 /\ s = s0 /\ time = -1.
PLANE(x,y,s) <=> [](x' = 220*cos(s)) /\ [](y' = 220*sin(s)).

CONST(s) <=> [](s' = 0).

COLLISION(x1,y1,x2,y2) <=>
[]( (x1- - x2-)^2 + (y1- - y2-)^2 = 30^2 =>  $FALSE).

AVOID_START(x1,y1,s1,t1,x2,y2) <=>
[]( (x1- - x2-)^2 + (y1- - y2-)^2 = (30+5000)^2 /\ t1- < 0
 =>  s1 = s1- + Pi/2 /\ t1 = 2*Pi).

AVOIDING(time,s) <=> []( time- > 0 => s' = -1/2).

AVOID_END(time,s,s0) <=> []( time- = 0 => s = s0-).

TIME(time)<=>[](time' = -1).

{ INIT(X[i],Y[i],S[i],Time[i],Xinit[i],Yinit[i],Sinit[i]) | i in {1..|X|} },
{ PLANE(X[i],Y[i],S[i]) | i in {1..|X|} },
{ COLLISION(X[i],Y[i],X[j],Y[j]) | i in {1..|X|-1}, j in {i+1..|X|} }.
{ (CONST(S[i]),TIME(Time[i])) << AVOID_START(X[i],Y[i],S[i],Time[i],X[j],Y[j]) | i in {1..|X|}, j in {1..|X|}, i!=j }.
{ CONST(S[i]) << AVOIDING(Time[i],S[i]) | i in {1..|S|} }.
{ CONST(S[i]) << AVOID_END(Time[i],S[i],Sinit[i]) | i in {1..|S|} }.
