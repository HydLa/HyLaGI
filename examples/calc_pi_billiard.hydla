// Initial setting
INIT_LOCATION <=> x1 = 0 & x2 = 10.
INIT_VELOCITY <=> x1' = 10 & x2' = 0.
INIT_COUNT <=> count = 0.

// Constraints
MASS <=> m1 = 100 & [](m1' = 0 & m2 = 1). // mass of each ball
CoR <=> [](cor = 1). // coefficient of restitution
ACCEL(x) <=> [](x'' = 0). // balls move at constant velocities
COUNT <=> [](count' = 0).

WALL(x) <=> [](x- = 20 => x' = -x'- & count = count- + 1). // balls bounce at x = 20
COLLISION <=> [](x1- = x2- =>
	(x1' = (-x1'- + x2'-) * (1 + cor) / (m1 / m2 + 1) + x1'-) &
	(x2' = (-x2'- + x1'-) * (1 + cor) / (m2 / m1 + 1) + x2'-) &
	(count = count- + 1)).

// Hierarchy
INIT_LOCATION, INIT_VELOCITY, INIT_COUNT,
MASS, CoR, (ACCEL(x1), COUNT) << WALL(x1), (ACCEL(x2), COUNT) << WALL(x2),
(ACCEL(x1) , ACCEL(x2), COUNT) << COLLISION.

