
INIT(x,y,v,s,time,x0,y0,v0,s0) <=> 
    x = x0 & y = y0 & x' = v0*cos(s0) & y' = v0*sin(s0) & s = s0 & v = v0 & time = 0.

ANGLE <=> [](angle = PI/6).
HUMAN_RADIUS <=> [](radius = 0.5).

TURN_LEFT(x1,y1,v1,s1,t1,r,x2,y2) <=>
    [](((x1- + r-*cos(s1-)) - x2-)^2 + 
       ((y1- + r-*sin(s1-)) - y2-)^2 <= (r-+(2*radius-))^2 &
       (cos(s1-) * (y2- - y1-) - (x2- - x1-) * sin(s1-)) <= 0 &
       t1- <= 0 & v1- > 0
                   => s1 = s1- + angle- & t1 = 5).

TURN_RIGHT(x1,y1,v1,s1,t1,r,x2,y2) <=>
    [](((x1- + r-*cos(s1-)) - x2-)^2 + 
       ((y1- + r-*sin(s1-)) - y2-)^2 <= (r-+(2*radius-))^2 &
       (cos(s1-) * (y2- - y1-) - (x2- - x1-) * sin(s1-)) > 0 &
       t1- <= 0 & v1- > 0
                   => s1 = s1- - angle- & t1 = 5).

COLLISION(x1,y1,v1,x2,y2,v2) <=>
    []((x1- - x2-)^2 + (y1- - y2- )^2 = (2*radius-)^2 & 
       (v1- > 0 | v2- > 0)
                   => v1 = 0 & v2 = 0 ).

CONST(vs) <=> [](vs'=0).
TIMER(time) <=> [](time' = -1).
VELOCITY(x,y,v,s) <=> [](x' = v*cos(s) & y' = v*sin(s)).

x <=> [2].
y <=> [2].
v <=> [2].
s <=> [2].
t <=> [2].

x0 <=> {100,70}.
y0 <=> {25,-20}.
v0 <=> {5,6}. 
s0 <=> {PI,PI/2}.

ANGLE, HUMAN_RADIUS,
{ INIT(x[i], y[i], v[i], s[i], t[i], x0[i], y0[i], v0[i], s0[i]) 
        | i in 0..|x|-1 }.

{ (TIMER(t[i]),CONST(s[i])) << TURN_LEFT(x[i],y[i],v[i],s[i],t[i], 1, x[j], y[j]) << $TRUE
        | i in 0..|x|-1, j in 0..|x|-1, 
          x[i] =!= x[j], y[i] =!= y[j] },

{ (TIMER(t[i]),CONST(s[i])) << TURN_RIGHT(x[i],y[i],v[i],s[i],t[i], 1, x[j], y[j]) << $TRUE
        | i in 0..|x|-1, j in 0..|x|-1, 
          x[i] =!= x[j], y[i] =!= y[j] },

{ (CONST(v[i]),CONST(v[j])) << COLLISION(x[i],y[i],v[i],x[j],y[j],v[j])
        | i in 0..|x|-2, j in i+1..|x|-1 }, 

{ VELOCITY(x[i],y[i],v[i],s[i]) | i in 0..|x|-1 }.

{ TIMER(t[i]) | i in 0..|t|-1 }.

